#!/usr/bin/nu

use bf

# Extract and install S6 overlay
def main [] {
    # delete default /init - will be replaced by S6
    bf del force /init

    # extract and install files
    bf write "Installing S6 Overlay."
    cd /s6
    extract noarch
    extract (bf fs read ARCH)

    # set ownership of src directory
    bf write "Setting ownership of src directory."
    bf ch --mode 1777 /etc/bf/src

    # link bf-withenv for easier use in shell scripts
    bf write "Creating withenv link."
    ln $"/usr/bin/bf/bf-withenv" /bin/withenv

    # add bf-s6 module to config
    bf config use bf-s6

    # cleanup
    bf write "Removing installation files."
    cd /
    bf del force /s6
}

# Shared code to extract arch and noarch files in the same way
def extract [
    arch: string    # Architecture to extract
] {
    # build file path
    let path = $"s6-overlay-($arch).tar.xz" | path expand

    # check integrity
    { ^sha256sum -cs $"($path).sha256" } | bf handle -f {|code, err|
        bf write error " .. S6 Overlay download does not match checksum." install/extract
    } install/extract

    # extract
    bf write $" .. extracting ($path)..."
    { ^tar -C / -Jxpf $path } | bf handle -f {|code, err|
        bf write error $" .. unable to extract ($path)." install/extract
    } install/extract
}
