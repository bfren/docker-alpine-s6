#!/usr/bin/nu

use bf

# Extract and install S6 overlay
def main [] {
    # extract and install files
    bf write "Installing S6 Overlay..."
    cd /s6
    extract
    extract (bf fs read ./arch)

    # cleanup
    bf write "Removing installation files..."
    cd /
    bf del force /s6
}

# Shared code to extract arch and noarch files in the same way
def extract [
    arch?: string = "noarch"    # Architecture to extract
] {
    # build file path
    let path = $"s6-overlay-($arch).tar.xz" | path expand

    # check integrity
    try {
        sha256sum -cs $"($path).sha256"
    } catch {
        bf write error " .. S6 Overlay download does not match checksum."
    }

    # extract
    bf write $" .. extracting ($path)..."
    try {
        tar -C / -Jxpf $path
    } catch {
        bf write error $" .. unable to extract ($path)."
    }
}
